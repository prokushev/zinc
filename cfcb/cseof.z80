;
;    CFCB library: An I/O library for CP/M giving byte-level access to files
;    Copyright (C) 2000 John Elliott <jce@seasip.demon.co.uk>
;
;    This library is free software; you can redistribute it and/or
;    modify it under the terms of the GNU Library General Public
;    License as published by the Free Software Foundation; either
;    version 2 of the License, or (at your option) any later version.
;
;    This library is distributed in the hope that it will be useful,
;    but WITHOUT ANY WARRANTY; without even the implied warranty of
;    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
;    Library General Public License for more details.
;
;    You should have received a copy of the GNU Library General Public
;    License along with this library; if not, write to the Free
;    Software Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
;
	.z80
	cseg
	
	extrn	c@wrec,c@xtnd,c@rrec,cgeof

cseof::	push	iy
	push	ix
	push	de
	pop	iy
	bit	7,(iy+47)	;Buffer dirty?
	scf
	push	hl
	ld	l,(iy+38)
	ld	h,(iy+39)
	call	nz,c@wrec	;Write it.
	pop	hl
	jp	nc,cskret
	push	bc
	push	hl
	call	cgeof
	jp	nc,popret	;Failed to get EOF position.
	ld	ix,0
	add	ix,sp		;IX+0...3 = position to set.
	ld	a,b
	cp	(ix+3)		;If it is less, truncate the file.
	jr	c,trunc
	jr	nz,xtnd		;If it is more, extend the file.
	ld	a,c
	cp	(ix+2)
	jr	c,trunc
	jr	nz,xtnd
	ld	a,h
	cp	(ix+1)
	jr	c,trunc
	jr	nz,xtnd
	ld	a,l
	cp	(ix+0)
	jr	c,trunc
	jr	nz,xtnd
	scf
	jr	popret		;Nothing to do, sizes are the same!	
;
trunc:	pop	hl
	pop	bc
	ld	a,l
	and	7fh		;A=bytelen modifier
	sla	l
	rl	h
	rl	c
	rl	b
	ld	l,a		;L=bytelen
	ld	a,b
	cp	4
	ld	a,21		;Sorry, pointer > 32Mb.
	jp	nc,cskret
	ld	a,b
	cp	(iy+46)
	jr	nz,cp01
	ld	a,c
	cp	(iy+45)
	jr	nz,cp01
	ld	a,h
	cp	(iy+44)
	jr	nz,cp01
;
;File sizes are the same except for the bytelen value.
;
	ld	(iy+36),l
	scf
	jr	cskret
;
cp01:	ld	(iy+46),b
	ld	(iy+45),c
	ld	(iy+44),h	;Record number set.
	ld	(iy+36),l	;Offset set.
	
	call	c@xtnd		;Was the file extended?
	call	c@rrec		;Attempt to read the new record
	jr	c,popret
	cp	25
	scf
	ccf	
	jr	nz,popret
;
;If it was an EOF, fill the buffer with 1Ah characters.
;
	push	de
	ld	l,(iy+38)
	ld	h,(iy+39)
	ld	d,h
	ld	e,l
	inc	de
	ld	bc,7fh
	ld	(hl),1ah
	ldir
	pop	de
	scf
popret:	pop	bc
	pop	hl	
cskret:	pop	ix
	pop	iy
	ret

xtnd:	ld	de,str
	ld	c,9
	call	5
	rst	0
str:	defb	'E_NOTIMPL$'
	end
