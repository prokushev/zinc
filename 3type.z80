;
;    3TYPE: Display text file (GPLed clone of CP/M 3 TYPE)
;    Copyright (C) 2000  John Elliott <jce@seasip.demon.co.uk>
;
;    This program is free software; you can redistribute it and/or modify
;    it under the terms of the GNU General Public License as published by
;    the Free Software Foundation; either version 2 of the License, or
;    (at your option) any later version.
;
;    This program is distributed in the hope that it will be useful,
;    but WITHOUT ANY WARRANTY; without even the implied warranty of
;    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;    GNU General Public License for more details.
;
;    You should have received a copy of the GNU General Public License
;    along with this program; if not, write to the Free Software
;    Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
;
	.z80
	cseg
;
;ZINC version of TYPE.COM
;
;
CPM3	EQU	1		; CP/M 3 check
CPNET	EQU	0		; CP/NET check
Z80	EQU	1		; Z80 check
;
	INCLUDE	ZINCHEAD.INC

	extrn	cparse,copen,crbyti,cerror,crclos,cexwild,@LAST
	extrn	c3pini,c3pout,c3opt,c3pwd,cpwset,copfcb,c3exit
	extrn	print

MAIN:	
	LD	HL,MYPWD
	CALL	CPWSET

	LD	C,2Dh	
	LD	E,0FFh		;Use CFCB to handle errors
	CALL	BDOS


	LD	DE,OPTS		;Check for PAGE / NOPAGE options
	LD	HL,80h
	LD	IY,OPTCS
	CALL	C3OPT

	LD	A,(PGMODE)
	CALL	C3PINI		;Initialise the pager

	LD	HL,80h		;Copy command line out of the way of
	LD	DE,@LAST+400h	;the error reporter, which uses the buffer
	LD	BC,128		;at 80h as workspace.
	LDIR

	LD	HL,@LAST+400h	;Expand wildcards in the command line.
	LD	DE,TYPF		;Subroutine to call if wildcards found
	LD	BC,PRMPT
	LD	IX,@LAST	;Buffer to use for expansion
	LD	A,9
	CALL	CEXWILD		;Do the expansion...
	LD	A,B
	OR	C		;No. of files _found_. If nonzero...
	JP	NZ,C3EXIT	;then return number of files successfully 
				;displayed.
	CALL	PRINT
	DEFB	'No files found.',13,10,0
	LD	HL,0FF00h
	JP	C3EXIT
;
pgon:	ld	a,1		;[PAGE]
	ld	(pgmode),a
	ret
;
pgoff:	xor	a		;[NOPAGE]
	ld	(pgmode),a
	ret
;
	dseg
OPTS:	defb	'P',0,'NO',0,0	;Options P(age) and NO(page)
OPTCS:	defw	pgon,pgoff

PGMODE:	defb	2
prmpt:	defb	'File to display: $'
PCFCB:	defw	0
	cseg
	
TYPF:	LD	HL,@last
	INC	H
	INC	H
	EX	DE,HL
	PUSH	DE
	LD	BC,48
	LDIR	
	CALL	PGCRLF
	POP	DE
	LD      HL,0
	LD	C,1
	CALL	COPFCB
	LD	A,':'
	CALL	C3POUT
	CALL	PGCRLF
	LD	BC,256
	LD	IX,@last
	ADD	IX,BC	
	LD	C,1
	CALL	COPEN
	JR	NC,CERR1
CTYP2:	CALL	CRBYTI
	JR	NC,CBYTIE
	LD	A,C
	CP	1Ah	;EOF, CP/M-2 style
	JR	Z,CBYTIF
	LD	A,C
	CALL	C3POUT
	JR	CTYP2
;
CBYTIE:	CP	25	;EOF
	JR	NZ,CERR1
CBYTIF:	CALL	CRCLOS
	JP	NC,CERR1
	LD	A,1	;File processed
	SCF
	RET
;
CERR1:	ld	de,@last
	inc	d
	inc	d
	call	cerror
	ld	hl,84h
	ld	(80h),hl
	dec	hl
	ld	(hl),0
	ld	de,80h
	ld	c,6Fh
	call	5
	CALL	PGCRLF
	XOR	A	;File not processed
	scf
	ret
;
;Reset the pager before asking for a password; since the password 
;input routine can print as many lines as it likes.
;
MYPWD:	LD	A,(PGMODE)
	CALL	C3PINI		;Initialise the pager
	JP	C3PWD
;
PGCRLF:	LD	A,0Dh		;Print a paged CR, LF.
	CALL	C3POUT
	LD	A,0Ah
	JP	C3POUT

	end





