;
; SYSLIB Module Name:  SPFN2
; Author:  Richard Conn
; SYSLIB Version Number:  3.6
; Module Version Number:  1.1

	public	pfn2

	ext	cout

;
;  Print FCB file name and type pted to by DE on CON:
;  Format of Output:  xxxxxxxx.yyy (0-8 x's, 0-3 y's)
;
pfn2:
	push	de	; save regs
	push	bc
	push	af
	ld	b,8	; 8 chars first
	call	prfnx
	ld	a,'.'	; dot
	call	cout
	ld	b,3	; 3 more chars
	call	prfnx
	pop	af	; restore regs
	pop	bc
	pop	de
	ret
prfnx:
	ld	a,(de)	; get char
	and	7fh	; mask out msb
	cp	' '	; done?
	call	nz,cout	; print it
	inc	de	; pt to next
	dec	b	; count down
	jp	nz,prfnx
	ret

	end
