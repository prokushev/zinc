;
; SYSLIB Module Name:  SMA2HC
; Author:  Richard Conn
; SYSLIB Version Number:  3.6
; Module Version Number:  1.1

	public	ma2hc
;
;  MA2HC --
;	STORE REG A AS 2 HEXADECIMAL CHARACTERS IN MEMORY PTED TO BY DE;
; ON INPUT, A=VALUE AND HL=PTR TO 2-BYTE BUFFER
; ON RETURN, DE PTS TO NEXT BYTE AFTER BUFFER
;
	EXT	EN

MA2HC:
	EX	DE,HL	; PTR IN HL INSTEAD
	PUSH	AF	; SAVE A
	PUSH	AF
	CALL	EN	; EXCHANGE NYBBLES
	CALL	PAHC	; PRINT LOW-ORDER NYBBLE AS HEX
	POP	AF	; GET A
	CALL	PAHC	; PRINT LOW-ORDER NYBBLE AS HEX
	POP	AF	; RESTORE A
	EX	DE,HL	; POINTER BACK IN DE
	RET
PAHC:	AND	0FH	; MASK FOR LOW NYBBLE
	CP	10	; LETTER OR DIGIT?
	JP	C,PADIG	; DIGIT IF CARRY
	ADD	'A'-10	; CONVERT TO 'A'-'F'
	JP	PUT	; PUT IN MEMORY
PADIG:	ADD	'0'	; CONVERT TO '0'-'9'
PUT:	LD	(HL),A	; CHAR IN MEMORY
	INC	HL	; PT TO NEXT LOCATION
	RET

	END
