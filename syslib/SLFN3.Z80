;
; SYSLIB Module Name:  SLFN3
; Author:  Richard Conn
; SYSLIB Version Number:  3.6
; Module Version Number:  1.1

	public	lfn3

	ext	lout

;
;  Print FCB file name and type pted to by DE on LST:
;  Format of Output:  xxxxxxxx.yyy <spaces> (0-8 x's, 0-3 y's, req'd spaces)
;
lfn3:
	push	de	; save regs
	push	bc
	push	af
	ld	c,11	; 11 chars total
	ld	b,8	; 8 chars first
	call	prfnx
	ld	a,'.'	; dot
	call	lout
	ld	b,3	; 3 more chars
	call	prfnx
	ld	a,c	; get count of spaces
	or	a	; 0=none
	call	nz,spacer
	pop	af	; restore regs
	pop	bc
	pop	de
	ret
prfnx:
	ld	a,(de)	; get char
	and	7fh	; mask out msb
	cp	' '	; skip space
	call	nz,prout	; print it
	inc	de	; pt to next
	dec	b	; count down
	jp	nz,prfnx
	ret
prout:
	dec	c	; count chars
	jp	lout	; print char
spacer:
	ld	a,' '	; space over
	call	lout
	dec	c	; count down
	jp	nz,spacer
	ret

	end
