;
; SYSLIB Module Name:  SCRC2
; Author:  Richard Conn
; SYSLIB Version Number:  3.6
; Module Version Number:  1.1

	public	crc2clr,crc2done,crc2upd

;
;     	These subroutines will compute and check a true 16-bit
;	Cyclic Redundancy Code for a message of arbitrary length.
;
;	The  use  of this scheme will guarantee detection of all
;	single and double bit errors, all  errors  with  an  odd
;	number  of  error bits, all burst errors of length 16 or
;	less, 99.9969% of all 17-bit error bursts, and  99.9984%
;	of  all  possible  longer  error bursts.  (Ref: Computer
;	Networks, Andrew S.  Tanenbaum, Prentiss-Hall, 1981)
;
;	These routines are typically used as follows:
;	  CRC$MAKE:		; ROUTINE TO ESTABLISH CRC VALUE
;		CALL	CRC2CLR	; CLEAR CRC
;		<loop CALLing CRC2UPD>	; ACQUIRE VALUES
;		CALL	CRC2DONE	; GET VALUE
;		SHLD	CRCVAL	; SAVE VALUE
;	  CRC$CHECK:		; ROUTINE TO CHECK CRC VALUE
;		CALL	CRC2CLR	; CLEAR CRC
;		<loop CALLing CRC2UPD>	; ACQUIRE VALUES
;		CALL	CRC2DONE	; NOW DONE
;		XCHG		; DE=RETURNED CRC
;		LHLD	CRCVAL	; FROM BEFORE
;		CALL	COMPHD	; COMPARE HL TO DE FOR EQUALITY
;		JNZ	ERROR	; ERROR IF NOT EQUAL
;

;
; CRC2CLR - Clear the CRC accumulator
;	 This routine must be called at the start of each byte stream.
;
;	 Input Parameters: None
;
;	 Output Parameters: None
;
CRC2CLR:
	PUSH	HL
	LD	HL,0	;SET CRC TO ZERO
	LD	(CRCVAL),HL
	POP	HL
	RET

;
;  BUFFER FOR CRC VALUE
;
CRCVAL:	DS	2

;
; CRC2UPD - Update the CRC accumulator
;	 This routine must be called once for each byte in the
;	 byte stream for which the CRC is being calculated.
;
;	 Input Parameters: A = byte to be included in CRC
;
;	 Output Parameters: None
;
;	Adapted from Keith Petersen's CRCK 4.2 program.
;	Routine Originally By Fred Gutman.
;	From 'EDN' magazine, June 5, 1979 issue, page 84.
;
CRC2UPD:
	PUSH	HL	;SAVE HL
	PUSH	BC	;SAVE BC
	PUSH	AF	;SAVE BYTE TO UPDATE
	LD	B,A	;SAVE BYTE IN B
	LD	HL,(CRCVAL)	;GET REMAINDER
	LD	A,H
	AND	128	;Q-BIT MASK
	PUSH	AF	;SAVE STATUS
	ADD	HL,HL	;2 X R(X)
	LD	A,B	;GET BYTE
	ADD	A,L
	LD	L,A
	POP	AF
	JP	Z,CRCU1	;IF Q-BIT IS ZERO
;
	LD	A,H
	XOR	0A0H	;MS HALF OF GEN. POLY
	LD	H,A
	LD	A,L
	XOR	97H	;LS HALF OF GEN. POLY
	LD	L,A
;
CRCU1:
	LD	(CRCVAL),HL	;SAVE RESULT
	POP	AF	;RESTORE REGS
	POP	BC
	POP	HL
	RET

;
; CRC2DONE - Complete the CRC calculation
;	This routine is called after the last byte of the byte stream
;	has passed thru CRCUPD, and it returns the calculated
;	CRC bytes, which must be transmitted as the final
;	two bytes of the message (first H, then L).
;
;	 Input Parameters: None
;
;	 Output Parameters:  HL = calculated CRC bytes
;
CRC2DONE:
	LD	HL,(CRCVAL)	;RETURN CRC VALUE IN HL
	RET

	END
